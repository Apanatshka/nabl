module coq-semantics/patterns

imports
  signatures/NameBindingLanguage-sig
  generation/util
  coq-semantics/Coq

rules
  
  pattern-to-cons =
    ?NoAnnoList(Op(<id>, _))

  patterns-to-term(|i, x): [] -> (Ref("Nop"), [], i, None())
  
  patterns-to-term(|i, x):
    [c|cs] -> (Apply(Apply(Ref("Consp"), ct), cts), [cbind*, csbind*], k, ret)
    with
      (ct, cbind*, j, hd) := <pattern-to-term(|i, x)> c
    ; (cts, csbind*, k, tl) := <patterns-to-term(|<inc> j, x)> cs
    ; ret := <?Some(_) <+ !tl> hd

  // (Co cC [c1; c2; c3] k)
  pattern-to-term(|i, x):
    NoAnnoList(Op(c, term*)) -> (Apply(Apply(Apply(Ref("Co"), Ref($[[c]C])), child*), Ref(k)), binder*, j, maybe)
    with
      k := $[k[i]]
    ; (child*, cbinder*, j, maybe) := <patterns-to-term(|<inc> i, x)> term*
    ; binder* := [cbinder*, Bind(k)]
      
  pattern-to-term(|i, x):
    Var(n) -> (Ref(n), [Bind(n)], i, None())
    where not(<eq> (x,Some(n)))
  
  pattern-to-term(|i, x):
    Var(n) -> (Apply(Apply(Ref("Id"), Ref(n)), Ref(k)), [Bind(k)], <inc> i, Some(k))
    where
      <eq> (x,Some(n))
    ; k := $[k[i]]  

  pattern-to-term(|i, x):
    ListVar(n) -> (Ref(n), [Bind(n)], i, None())

  pattern-to-term(|i, x):
    Wld() -> (Ref(n), [Bind(n)], <inc> i, None())
    with
      n := $[wld[i]]

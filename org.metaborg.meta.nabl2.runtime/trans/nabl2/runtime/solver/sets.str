module nabl2/runtime/solver/sets

imports
 
  signatures/nabl2/shared/constraints/-
  nabl2/shared/-

  nabl2/runtime/analysis/-
  nabl2/runtime/common/-
  nabl2/runtime/prelude/-
  nabl2/runtime/solver/-

rules
 
  nabl2--solve-constraint = nabl2--time-analysis(!"sets",nabl2--solve-set-constraint)

  nabl2--solve-set-constraint: (CDistinct(x*,msginfo),sol) -> [(sol',[])]
  where <nabl2--is-ground> x*
  with x'* := <nabl2--bag-dups> x*
     ; ( ?[]
       < !sol
       + msg := "Duplicate"
       ; if msginfo* := <nabl2--apply-elems-message(|x'*)> msginfo then
           <nabl2--add-messages(nabl2--default-message(|msg)|msginfo*)> sol
         else
           <nabl2--add-message(nabl2--default-message(|msg)|msginfo)> sol
         end
       ) => sol'

  nabl2--solve-set-constraint: (CSubsetEq(x1*,x2*,msginfo),sol) -> [(sol',[])]
  where <nabl2--is-ground> x1*
      ; <nabl2--is-ground> x2*
  with x'* := <nabl2--bag-diff;nabl2--bag2set> (x1*,x2*)
     ; ( ?[]
       < !sol
       + msg := "Missing"
       ; if msginfo* := <nabl2--apply-elems-message(|x'*)> msginfo then
           <nabl2--add-messages(nabl2--default-message(|msg)|msginfo*)> sol
         else
           <nabl2--add-message(nabl2--default-message(|msg)|msginfo)> sol
         end
       ) => sol'

  nabl2--constraint-priority: CDistinct(_,_) -> 3
  nabl2--constraint-priority: CSubsetEq(_,_,_) -> 3

rules

  nabl2--postprocess-constraint = nabl2--postprocess-set-constraint

  nabl2--postprocess-set-constraint: (CDistinct(s, msginfo), sol) -> sol'
  with msg := ["Failed to check if elements in ",s," are distinct"];
       sol' := <nabl2--add-message(nabl2--default-message(|msg)|msginfo)> sol

  nabl2--postprocess-set-constraint: (CSubsetEq(s1, s2, msginfo), sol) -> sol'
  with msg := ["Failed to check if ",s1," is a subset of or equal to ",s2];
       sol' := <nabl2--add-message(nabl2--default-message(|msg)|msginfo)> sol

rules

  nabl2-reduce-op-hook(|sol): EmptySet() -> []

  nabl2-reduce-op-hook(|sol): Isect(s1,s2) -> <nabl2--bag-isect> (s1,s2)
    where <nabl2--is-ground> s1;
          <nabl2--is-ground> s2

  nabl2-reduce-op-hook(|sol): Union(s1,s2) -> <nabl2--bag-union> (s1,s2)
    where <nabl2--is-ground> s1;
          <nabl2--is-ground> s2

  nabl2-reduce-op-hook(|sol): Diff(s1,s2) -> <nabl2--bag-diff> (s1,s2)
    where <nabl2--is-ground> s1;
          <nabl2--is-ground> s2
 
  nabl2--apply-elems-message(|x*) = fail

module nabl2/runtime/solver/uninterpreted

imports

  nabl2/shared/-
  nabl2/runtime/analysis/-
  nabl2/runtime/common/-
  nabl2/runtime/prelude/-
  nabl2/runtime/solver/-

rules
 
  nabl2--solve-constraint =nabl2--time-analysis(!"uninterpreted",nabl2--solve-uninterpreted-constraint)

  nabl2--solve-uninterpreted-constraint: (CGoal(t),sol) -> [(sol',[])]
    where <nabl2--is-ground> t
    with sol' := <nabl2--sol-add-u-goal(|t)> sol

  nabl2--solve-uninterpreted-constraint: (CFact(t),sol) -> [(sol',[])]
    where <nabl2--is-ground> t
    with sol' := <nabl2--sol-add-u-fact(|t)> sol

rules

  nabl2--postprocess-constraint = nabl2--postprocess-uninterpreted-constraint

  nabl2--postprocess-uninterpreted-constraint: (c@CGoal(t), sol) -> sol'
  with msg := ["Failed to add goal ",t,", because it is not ground"];
       sol' := <nabl2--add-message> (Error(), (c, msg), sol)

  nabl2--postprocess-uninterpreted-constraint: (c@CFact(t), sol) -> sol'
  with msg := ["Failed to add fact ",t,", because it is not ground"];
       sol' := <nabl2--add-message> (Error(), (c, msg), sol)

rules

  nabl2--constraint-priority: CGoal(_) -> 99
  nabl2--constraint-priority: CFact(_) -> 99


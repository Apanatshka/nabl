module nabl2/runtime/solver/errors

imports

  libspoofax/term/origin

  signatures/nabl2/shared/common/-
  signatures/nabl2/shared/constraints/-
  nabl2/shared/-

  signatures/nabl2/runtime/common/-
  nabl2/runtime/-
  nabl2/runtime/common/-
  nabl2/runtime/prelude/-
  nabl2/runtime/solver/-

rules

  nabl2--detail-messages(|detailmsg) = map(nabl2--detail-message(|detailmsg))

  nabl2--detail-message(|detailmsg): Message(kind,msg,pos) -> Message(kind,msg',pos)
    with if Default() := msg then
           msg' := <nabl2--sanitize-message> detailmsg
         else
           msg' := Formatted([<nabl2--sanitize-message> msg, Text(" ("), <nabl2--sanitize-message> detailmsg, Text(")")])
         end

  nabl2--default-messages(|defaultmsg) = map(nabl2--default-message(|defaultmsg))

  nabl2--default-message(|defaultmsg): Message(kind,msg,pos) -> Message(kind,msg',pos)
    with if Default() := msg then
           msg' := <nabl2--sanitize-message> defaultmsg
         else
           msg' := <nabl2--sanitize-message> msg
         end

  nabl2--add-messages(s|msginfo*): sol -> sol'
    with sol' := <foldr(!sol,\ (msginfo,sol) -> <nabl2--add-message(s|msginfo)> sol \)> msginfo*

  nabl2--add-message(s|msginfo): sol -> sol'
    with Message(kind,msg,pos) := <s> msginfo
    with msg' := <nabl2--sanitize-message;pp-NaBL2-MessageContent> msg;
         sol' := <nabl2--add-message> (kind,(pos,msg'),sol)

  nabl2--add-message: (Error(), error, sol) -> <nabl2--sol-add-error(|error)> sol
  nabl2--add-message: (Warning(), warning, sol) -> <nabl2--sol-add-warning(|warning)> sol
  nabl2--add-message: (Note(), note, sol) -> <nabl2--sol-add-note(|note)> sol

  nabl2--message-eq =
    where(eq;(Fst;origin-location-offset,Fst;origin-location-offset);eq)

rules

  nabl2--sanitize-message =
    ?Default()
 <+ ?Formatted(_)
 <+ ?Text(_)
 <+ ?Term(_)
 <+ !Text(<is-string>)
 <+ !Formatted(<is-list;map(nabl2--sanitize-message)>)
 <+ !Term(<id>)

rules

  nabl2--add-unsolved-constraint-error =
    ?(c,sol);
  nabl2--strict(
    msg := Message(Error(),$[Unsolved constraint: [<pp-NaBL2-Constraint> c]],None());
    <nabl2--add-message(id|msg)> sol
  )


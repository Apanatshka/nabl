module nabl2/runtime/solver/base

imports

  nabl2shared
  nabl2/runtime/common/-
  nabl2/runtime/solver/-

rules
	
  solve-fact = fail
  
  solve-constraint = fail	
	
  constraint-priority = fail	
	
rules

  initial-solution = sol-new

	solve-ignore-placeholders: (c,sol) -> [(sol,[])]
	where <is-NaBL2-PrimitiveConstraint;
	       has-placeholder> c

  has-placeholder =
    not(?Message(_,_,_));
    (is-SDF-placeholder <+ one(has-placeholder))

  solve-constraints-phase1 =
    csolve-greedy( solve-specialise
                 , solve-ignore-placeholders + solve-fact
                 , id
                 , solve-errors-lt
                 , solve-zero-errors
                 )

  solve-constraints-phase2(|rn*) =
    csolve-greedy( solve-specialise
                 , solve-ignore-placeholders + solve-constraint
                 , postprocess-errors(|rn*)
                 , solve-errors-lt
                 , solve-zero-errors
                 )

  solve-specialise: (sol,c) -> res
  with <sol-s> sol => s*
     ; <S-apply(is-var)> (s*,c) => c'
     ; {| ReduceErrors:
            rules(ReduceErrors : _ -> [])
          ; <bottomup(try(reduce-op(|sol)))> c' => c''
          ; re* := <ReduceErrors>
          ; if [] := re*
            then
              !(sol,c'')
            else
              msg' := <get-constraint-message> c'
            ; msg'' := <foldr(!msg',{ ?(re,<id>) ; detail-message(|re) })> re*
            ; sol' := <add-message(id|msg'')> sol
            ; !(sol',CTrue())
            end
       |} => res

  add-reduce-error(|msg) =
    where(msg* := <ReduceErrors> ; rules(ReduceErrors : _ -> [msg|msg*]))

  solve-errors-lt =
    let en = Fst ; sol-e ; length in (en,en) ; lt end

  solve-zero-errors =
   Fst ; sol-e ; length ; ?0 

rules
 
  solve-constraint: (CTrue(),sol) -> [(sol,[])]
  solve-constraint: (CFalse(msginfo),sol) -> [(sol',[])]
    with msg  := "False never succeeds";
         sol' := <add-message(default-message(|msg)|msginfo)> sol

  solve-fact: (CConj(c*),sol) -> [(sol,c*)]

  solve-constraint: (CConj(c*),sol) -> [(sol,c*)]
  solve-constraint: (CDisj(c*),sol) -> <map(\ c -> (sol,[c]) \)> c*
 
  constraint-priority: CTrue() -> 0
  constraint-priority: CFalse(_) -> 0
  constraint-priority: CConj(_) -> 0
  constraint-priority: CDisj(_) -> 100

rules

  sort-constraints = qsort((constraint-priority,constraint-priority);lt)

  constraint-priority-or-default = constraint-priority <+ 99

rules
 
  debug-solve(s): i@(c,sol) -> o*
    with <debug> $[solve [<pp-NaBL2-Constraint> c]]
    where if o* := <s> i then
            <with(map(Snd;debug;map(pp-NaBL2-Constraint);debug(!"--> ")))> o*
          else
            <debug> "--> (unsolved)"; fail
          end


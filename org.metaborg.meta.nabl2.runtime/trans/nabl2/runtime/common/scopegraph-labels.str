module nabl2/runtime/common/scopegraph-labels

imports

  nabl2/shared/-
  nabl2/runtime/prelude/-
  
rules

  // TODO: Calculate (and check) the closure over the label order. If some pairs
  // are not explicit, the resolution algorithm will currently fail.

  nabl2--labels    = ![D()|<nabl2--custom-labels <+ nabl2--default-labels>]
  nabl2--labels-wf = nabl2--custom-labels-wf <+ nabl2--default-labels-wf
  nabl2--label-lt  = nabl2--custom-label-lt + nabl2--default-label-lt

  nabl2--custom-labels    = fail
  nabl2--custom-labels-wf = fail
  nabl2--custom-label-lt = fail

rules

  nabl2--nullable:
    Epsilon() -> Epsilon()
  nabl2--nullable:
    Closure(_) -> Epsilon()
  nabl2--nullable:
    Concat(r1,r2) -> Epsilon()
    where Epsilon() := <nabl2--nullable> r1
        ; Epsilon() := <nabl2--nullable> r2
  nabl2--nullable:
    Or(r1,r2) -> Epsilon()
    where Epsilon() := <nabl2--nullable> r1
        + Epsilon() := <nabl2--nullable> r2
  nabl2--nullable:
    And(r1,r2) -> Epsilon()
    where Epsilon() := <nabl2--nullable> r1
        ; Epsilon() := <nabl2--nullable> r2
  nabl2--nullable:
    _ -> Empty()
 
  nabl2--emptiness = ?Empty()
  nabl2--emptiness = ?Concat(<nabl2--emptiness>,_) + ?Concat(_,<nabl2--emptiness>)
  nabl2--emptiness = ?Or(<nabl2--emptiness>,<nabl2--emptiness>)
  nabl2--emptiness = ?And(<nabl2--emptiness>,_) + ?And(_,<nabl2--emptiness>)

  nabl2--deriv(|l): l -> Epsilon()
  nabl2--deriv(|l): Closure(r) -> Concat(<nabl2--deriv(|l)> r, Closure(r))
  nabl2--deriv(|l): Concat(r1,r2) -> Or(Concat(<nabl2--deriv(|l)> r1, r2), Concat(<nabl2--nullable> r1, <nabl2--deriv(|l)> r2))
  nabl2--deriv(|l): Or(r1,r2) -> Or(<nabl2--deriv(|l)> r1, <nabl2--deriv(|l)> r2)
  nabl2--deriv(|l): And(r1,r2) -> And(<nabl2--deriv(|l)> r1, <nabl2--deriv(|l)> r2)
  nabl2--deriv(|l): _ -> Empty()
 
  // builtin labels
  nabl2--max-labels(lt) = nabl2--strict(
    ?L;
    filter(\ l -> l where <map(\ l' -> <not(lt)> (l,l') \)> L \);
    make-set
  )
 
  nabl2--labels-lt(lt|l) = nabl2--strict(
    filter(\ l' -> l' where <lt> (l',l) \);
    make-set
  )
 
  nabl2--default-labels   = ![P(),I()]
  
  nabl2--default-label-lt = ?(D(),I())
  nabl2--default-label-lt = ?(D(),P())
  nabl2--default-label-lt = ?(I(),P())
  
  nabl2--default-labels-wf = !Concat(Closure(P()),Closure(I()))

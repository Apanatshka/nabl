module nabl2/runtime/common/substitutions

imports

  nabl2/runtime/prelude/-
 
rules

  // nabl2--S-foldr(s0: s, s: a -> s) :: [a] -> s
  nabl2--S-foldr(isv,s0,s) = foldr(s0,nabl2--S-foldr-step(isv,s))
  // nabl2--S-foldr-step(s: a -> s) :: (a,s) -> s
  nabl2--S-foldr-step(isv,s): (t,s) -> s''
  where <nabl2--S-apply(isv);s> (s,t) => s'
      ; <nabl2--S-compose(isv)> (s',s) => s''


  nabl2--S-compose(isv): s* -> s
  where is-list
  with s := <foldr([],nabl2--S-compose(isv))> s*

  nabl2--S-compose(isv): (s',s) ->  <conc> (s', <nabl2--mapv(<nabl2--S-apply(isv)> (s',<id>))> s)


  nabl2--S-apply(isv): (s,t) -> t'
  with <topdown( try( isv ; <lookup> (<id>,s) ) )> t => t'


  nabl2--S-occurs(isv): (v,t) -> <id>
  where <sometd(?v)> t


  nabl2--S-unify(isv): (t,t) -> []

  nabl2--S-unify(isv): (v,t) -> [(v,t)]
    where <isv> v;
          <not(nabl2--S-occurs(isv))> (v,t)

  nabl2--S-unify(isv): (t,v) -> <nabl2--S-unify(isv)> (v,t)
    where <isv> v
 
  nabl2--S-unify(isv): (c#(a1*),c#(a2*)) -> s''
    where s'' := <zip;foldr(![],{\ ((t1,t2),s) -> <nabl2--S-compose(isv)> (s',s)
                                     where s' := <nabl2--S-apply(isv);nabl2--S-unify(isv)> (s,(t1,t2))
                                \})> (a1*,a2*)
